// releng: ./gradlew clean && ./gradlew uploadArchives -Prelease
// build: ./gradlew build

group 'me.geso.esmapper'
def packageVersion = '0.0.1'

//set build variables based on build type (release, continuous integration, development)
def isDevBuild
def isReleaseBuild
def sonatypeRepositoryUrl
if (hasProperty("release")) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else if (hasProperty("ci")) {
    packageVersion += "-SNAPSHOT"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
    isDevBuild = true
    packageVersion += "-SNAPSHOT"
}

apply plugin: 'java'

subprojects {
    apply plugin: 'java'
    apply plugin: "maven"
    apply plugin: "signing"

    version packageVersion

    repositories {
        mavenCentral()
        jcenter()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from "${buildDir}/javadoc"
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    compileJava {
        options.compilerArgs << '-XDignore.symbol.file' << '-Xlint:all'
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        required { isReleaseBuild }
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            if (isDevBuild) {
                mavenLocal()
            } else {
                mavenDeployer {
                    if (isReleaseBuild) {
                        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    }

                    repository(url: sonatypeRepositoryUrl) {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    }

                    pom.project {
                        name 'me.geso.esmapper'
                        packaging 'jar'
                        description 'Elasticsearch mapper'
                        url 'https://github.com/tokuhirom/esmapper'

                        scm {
                            url "scm:git@github.com:tokuhirom/esmapper.git"
                            connection "scm:git@github.com:tokuhirom/esmapper.git"
                            developerConnection "scm:git@github.com:tokuhirom/esmapper.git"
                        }
                        licenses {
                            license {
                                name 'MIT'
                                url 'https://opensource.org/licenses/MIT'
                            }
                        }
                        developers {
                            developer {
                                id 'tokuhirom'
                                name 'Tokuhiro Matsuno'
                                email 'tokuhirom@gmail.com'
                            }
                        }
                    }
                }
            }
        }
    }

}

project(":esmapper-core") {
    apply plugin: 'java'

    dependencies {
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.7.4'
        compile "org.elasticsearch:elasticsearch:2.3.3"
        compileOnly "org.projectlombok:lombok:1.16.8"

        testCompile 'org.apache.lucene:lucene-test-framework:5.5.0'
        testCompile "org.elasticsearch:elasticsearch:2.3.3:tests"
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
